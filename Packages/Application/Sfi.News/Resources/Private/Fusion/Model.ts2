#Display full view of article
prototype(Sfi.News:News) < prototype(Neos.Neos:Content) {
	@context.variant = ${q(node).property('variant') != 'Inherit' ? q(node).property('variant') : q(node).parent().property('variant')}
	templatePath = ${'resource://Sfi.News/Private/Templates/News' + q(node).parent().property('variant') + '.html'}
	partialRootPath = 'resource://Sfi.Sfi/Private/Partials'
	@context.configuration = ${Configuration.setting('Sfi.News.newsDetail')}
	configuration = ${configuration}

	title = ${q(node).property('title')}
	teaser = ${q(node).property('teaser')}
	lead = ${q(node).property('lead')}
	author = ${String.stripTags(q(node).property('author'))}
	authorPhoto = ${String.stripTags(q(node).property('authorPhoto'))}
	translator = ${String.stripTags(q(node).property('translator'))}
	date = ${q(node).property('date')}

	coverCollection = Neos.Neos:ContentCollection {
		nodePath = 'coverCollection'
		prototype(Neos.NodeTypes:Image) {
			maximumWidth = 980
			maximumHeight = 653
			allowCropping = TRUE
		}
		@if.notVideoOrBackend = ${(q(node).property('hasVideo') != true) || (node.context.workspace.name != 'live')}
	}
	gallery = Sfi.Shared:Carousel {
		content.collection = ${q(node).children('gallery').children()}
		attributes.class = 'Carousel js-carousel-2x'
		@if.hassGallery = ${q(node).children('gallery').children().count() > 0}
	}

	main = Neos.Neos:ContentCollection {
		nodePath = 'main'
	}
	assets = Neos.Neos:ContentCollection {
		nodePath = 'assets'
	}

	category = ${q(node).parent().get(0)}

	languageMenu = Neos.Fusion:Tag {
	    lang = ${Array.first(node.context.dimensions.language)}
	    @context.altLang = ${this.lang == 'ru' ? 'en' : 'ru'}
	    tagName = 'a'
	    attributes.class = 'Link Service--Small marginBottom-single'
		attributes.style = 'display: block'
		@context.altNode = ${q(documentNode).context({targetDimensions: {language: altLang}, dimensions: {language: [altLang]}})}
		@if.nodeExists = ${altNode.count() > 0}
	    attributes.href = Neos.Neos:NodeUri {
	        node = ${altNode.get(0)}
	    }
	    content = ${altLang == 'en' ? 'Read this in English' : 'Читать по-русски'}
	}


	#####
	moreItems = Sfi.News:NewsRelated {
		newsCollection = ${q(site).find('[instanceof Sfi.News:News]').get()}
	}
}

#Breif rendering of article, used in NewsList
prototype(Sfi.News:NewsShort) < prototype(Neos.Neos:Content) {
	templatePath = 'resource://Sfi.News/Private/Templates/NewsShort.html'

	title = ${String.stripTags(q(node).property('title'))}
	date = ${q(node).property('date')}
	teaser = ${String.stripTags(q(node).property('teaser'))}
	# first image found becomes the thumbnail image TODO: remove legacy support for coverPhoto
	thumbnailImage = Neos.Fusion:Case {
		hasCoverCollectionImage {
			condition = ${q(node).children('coverCollection').children().count() > 0}
			renderer = ${q(node).children('coverCollection').children().first().property('image')}
		}
		default {
			condition = ${true}
			renderer = ${null}
		}
	}
	# parrent node is the category
	category = ${q(node).parent().get(0)}

	@cache {
		mode = 'cached'
		entryIdentifier {
			node = ${node}
		}
		entryTags {
			1 = ${'Node_' + node.identifer}
			2 = ${'DescendantOf_' + node.identifer}
		}
	}
}


prototype(Sfi.News:TopShort) < prototype(Sfi.News:NewsShort) {
	templatePath = 'resource://Sfi.News/Private/Templates/TopShort.html'
	@context.configuration = ${Configuration.setting('Sfi.News.topShort')}
	configuration = ${configuration}

	place = ${String.stripTags(q(node).property('place'))}

	thumbnailImage.hasCoverImage {
		condition = ${node.properties.coverImage}
		renderer = ${node.properties.coverImage}
		@position = 'start'
	}

	@cache {
		mode = 'cached'
		entryIdentifier {
			node = ${node}
		}
		entryTags {
			1 = ${'Node_' + node.identifer}
			2 = ${'DescendantOf_' + node.identifer}
		}
	}
}


prototype(Sfi.News:Category) {
	@context.rootNode = ${q(documentNode).parent().is('[instanceof Sfi.News:Category]') ? q(documentNode).parent().get(0) : documentNode}
	@context.configuration = ${Configuration.setting('Sfi.News.' + q(q(documentNode).parent().is('[instanceof Sfi.News:Category]') ? q(documentNode).parent().get(0) : documentNode).property('preset'))}
	templatePath = 'resource://Sfi.News/Private/Templates/NewsCategory.html'
	partialRootPath = 'resource://Sfi.Sfi/Private/Partials'
	layoutRootPath = 'resource://Sfi.Sfi/Private/Layouts'

	layout = ${configuration.layout}

	sectionMenu = ${sectionMenu}
	newsList = Sfi.News:Archive
}
