include: **/*.fusion

prototype(Sfi.Shared:TextWithImage) {
	attributes.class = ${'TextWithImage ' + node.properties.variant}
	headline = Neos.NodeTypes:Headline {
		@override.node = ${q(node).children('headline').get(0)}
	}
	text = Neos.NodeTypes:Text {
		@override.node = ${q(node).children('text').get(0)}
	}
	image = Neos.NodeTypes:Image {
		@override.node = ${q(node).children('image').get(0)}
	}
	prototype(Neos.NodeTypes:Image) {
		maximumWidth = 600
	}
}

prototype(Sfi.Shared:Asset) {
	tooltip = Neos.Fusion:Component {
		renderer = afx`
		<svg width="16" height="16" viewBox="0 0 20 20" fill="none" xmlns="http://www.w3.org/2000/svg"><path fill-rule="evenodd" clip-rule="evenodd" d="M18 10a8 8 0 11-16 0 8 8 0 0116 0zm2 0c0 5.523-4.477 10-10 10S0 15.523 0 10 4.477 0 10 0s10 4.477 10 10zm-7-2a1 1 0 11-2 0 1 1 0 012 0zm2 0a3 3 0 01-4.293 2.708L9.414 12l.293.293a1 1 0 01-1.414 1.414L8 13.414l-.293.293a1 1 0 01-1.414-1.414l1-1 2-2A3 3 0 1115 8z" fill="#909EBB"/></svg>
		`
		renderer.@if.onlySigned = ${node.properties.variant == 'Asset-Signed'}
	}
	attributes.title = ${'Дата и время подписания: ' + Date.format(node.properties.signDate, 'Y.m.d H:i:s') +'&#013;ФИО подписавшего документ: ' + node.properties.signee + '&#013;Должность: ' + node.properties.signeePosition + '&#013;Уникальный программный ключ: ' + node.properties.asset.resource.sha1}
	attributes.title.@if.onlySigned = ${node.properties.variant == 'Asset-Signed'}
	attributes.class = ${node.properties.variant == 'Asset-Button' ? 'Button AssetButton' : 'ButtonInline AssetInline'}
	attributes.href = ${'asset://' + node.properties.asset.identifier}
	attributes.href.@process.convertUris = Neos.Neos:ConvertUris
	attributes.href.@if.notInBackend = ${!node.context.inBackend}
}

prototype(Sfi.Shared:Link) {
	attributes.class = Neos.Fusion:Case {
		linkButton {
			condition = ${node.properties.variant == 'Link-Button'}
			renderer = 'Button LinkButton'
		}
		linkButtonAlt {
			condition = ${node.properties.variant == 'Link-Button-Alt'}
			renderer = 'Button LinkButton Button--warn'
		}
		linkInline {
			condition = ${node.properties.variant == 'Link-Inline'}
			renderer = 'ButtonInline LinkInline'
		}
	}
	attributes.href = ${node.properties.target}
	attributes.href.@process.convertUris = Neos.Neos:ConvertUris {
		forceConversion = true
	}
	attributes.href.@if.notInBackend = ${!node.context.inBackend}
}

prototype(Sfi.Shared:ImageLink) {
	attributes.class = ${'ImageLink ' + node.properties.variant}
	maximumWidth = 300
	maximumHeight = 250
	allowCropping = TRUE
	allowUpScaling = TRUE
	link.@process.convertUris = Neos.Neos:ConvertUris {
		forceConversion = TRUE
	}
	link.@if.notInBackend = ${!node.context.inBackend}
}

prototype(Sfi.Shared:Group) < prototype(Neos.Fusion:Tag) {
	attributes.style = ${node.properties.style}
	attributes.class = ${'Container ' + node.properties.class}
	attributes.itemprop = ${node.properties.itemprop ? node.properties.itemprop : null}
	content = Neos.Neos:ContentCollectionRenderer {
		@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
	}
	@cache {
		mode = 'cached'
		entryIdentifier {
			node = ${node}
			editPreviewMode = ${editPreviewMode}
		}
		entryTags {
			1 = ${Neos.Caching.nodeTag(node)}
			2 = ${Neos.Caching.descendantOfTag(node)}
		}
	}
}

prototype(Sfi.Shared:Well) < prototype(Sfi.Shared:Group) {
	attributes.class = 'Well'
}


prototype(Sfi.Shared:ListByReferences) < prototype(Neos.Fusion:Tag) {
	@override.itemRendererType = ${q(node).property('itemRenderer')}
	content = Neos.Fusion:Collection {
		collection = ${q(node).property('references')}
		itemRenderer = Sfi.Shared:ContentCaseShort {
			byType {
				@position = 'start'
				condition = ${itemRendererType}
				type = ${itemRendererType}
			}
		}
		itemName = 'node'
		iterationName = 'iterator'
	}
	@cache {
		mode = 'cached'
		entryIdentifier {
			node = ${node}
			editPreviewMode = ${editPreviewMode}
		}
		entryTags {
			1 = ${Neos.Caching.nodeTypeTag('Flowpack.Listable:ListableMixin')}
			2 = ${Neos.Caching.nodeTag(node)}
		}
	}
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}
