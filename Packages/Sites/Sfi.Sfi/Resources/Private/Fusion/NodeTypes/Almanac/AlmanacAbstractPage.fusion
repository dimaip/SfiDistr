prototype(Sfi.Sfi:AlmanacAbstractPage) < prototype(Neos.Neos:Page) {
    @context.altLang = ${Array.first(node.context.dimensions.language) == 'ru' ? 'en' : 'ru'}
	head {
		titleTag.content = ${String.stripTags(q(node).property('title')) + ' | ' + Translation.translate('sfi.abbrev', null, [], null, 'Sfi.Sfi')}

		norobots = afx`<meta name="robots" content="noindex, nofollow"/>`
		norobots.@if.notLive = ${node.context.workspaceName != 'live'}

		stylesheets = Sfi.Sfi:Stylesheets

		javascripts.headScripts = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
			sectionName = 'headScripts'
		}
		javascripts.@cache {
			mode = 'cached'
			entryIdentifier.a = 'a'
		}
		opengraphImage = Sfi.Sfi:OpenGraphImage
	}
	htmlTag.attributes.lang = ${Array.first(node.context.dimensions.language)}
	bodyTag.attributes.class = 'Page'
	bodyTag.attributes.id = ${'page-' + q(node).property('_name')}
	body = Neos.Neos:ContentComponent {
		@context.almanacRoot = ${q(site).find('[instanceof Sfi.Sfi:AlmanacRootPage]').get(0)}
		javascripts.bodyScripts = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
			sectionName = 'bodyScripts'
		}
		javascripts.analytics = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
			partialRootPath = 'resource://Sfi.Sfi/Private/Partials'
			sectionName = 'analytics'
		}
		javascripts.@cache {
			mode = 'cached'
			entryIdentifier.a = 'a'
		}
	}
}
