prototype(Sfi.Sfi:PersonCard) < prototype(Neos.Fusion:Component) {
    name = Neos.Neos:Editable {
        property = 'name'
        @process.isTarget = ${node.properties.targetSource ? node.properties.target.properties.title : value}
    }
    description = Neos.Neos:Editable {
        property = 'description'
        @process.isTarget = ${node.properties.targetSource ? node.properties.target.properties.freeText : value}
    }
    link = Neos.Neos:NodeUri {
        node = ${q(node).property('target')}
    }
    imageUri = Neos.Neos:ImageUri {
        asset = ${node.properties.targetSource ? (node.properties.target.properties.squareImage || node.properties.target.properties.image) : node.properties.image}
        maximumWidth = 100
        maximumHeight = 100
        allowCropping = true
    }
    renderer = afx`
        <div class="marginVertical-triple" style="display: flex">
            <div class="marginRight-double" style="flex-shrink: 0">
                <img class="borderColor-gray" style="border-radius: 50%; border: 5px solid" src={props.imageUri}/>
            </div>
            <div>
                <p class="H4">{props.name}</p>
                <p class="ServiceEm margin-none">{props.description}</p>
            </div>
        </div>
    `
    renderer.@process.link = afx`<a href={props.link}>{value}</a>`
    renderer.@process.link.@if.notInBE = ${!node.context.inBackend && props.link}
    @process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}
