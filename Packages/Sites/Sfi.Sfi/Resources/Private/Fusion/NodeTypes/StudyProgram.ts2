prototype(Sfi.Sfi:StudyProgramAsset) <  prototype(Sfi.Shared:Asset) {
	attributes.class = 'ButtonInline AssetInline'
	attributes.href = ${'asset://' + node.properties.asset.identifier}
	attributes.href.@process.convertUris = Neos.Neos:ConvertUris
	@if.assetNotEmptyOrInBackend = ${node.properties.asset.identifier != null || documentNode.context.inBackend}
}

prototype(Sfi.Sfi:StudyProgram) {
	partialRootPath = 'resource://Sfi.Sfi/Private/Partials'

	sectionMenu = ${sectionMenu}
	level = ${String.stripTags(q(node).parent().parent().property('title'))}
	division = ${String.stripTags(q(node).parent().property('title'))}

	#TODO: translate
	assetDescription = Sfi.Sfi:StudyProgramAsset {
		title = 'Описание образовательной программы'
		@override.node = ${q(node).children('assetDescription').get(0)}
	}
	assetSchedule = Sfi.Sfi:StudyProgramAsset {
		title = 'Календарный учебный график'
		@override.node = ${q(node).children('assetSchedule').get(0)}
	}
	assetAccreditation = Sfi.Sfi:StudyProgramAsset {
		title = 'Свидетельство о государственной аккредитации'
		@override.node = ${q(node).children('assetAccreditation').get(0)}
	}
	assetInvoice = Sfi.Sfi:StudyProgramAsset {
		title = 'Квитанция для оплаты обучения'
		@override.node = ${q(node).children('assetInvoice').get(0)}
	}
	assetSampleContract = Sfi.Sfi:StudyProgramAsset {
		title = 'Образец договора об оказании платных образовательных услуг'
		@override.node = ${q(node).children('assetSampleContract').get(0)}
	}

	assetsPlan = Neos.Neos:ContentCollection {
		nodePath = 'assetsPlan'
	}
	assetsAdmissionRegulations = Neos.Neos:ContentCollection {
		nodePath = 'assetsAdmissionRegulations'
	}
	assetsSubjectAnnotations = Neos.Neos:ContentCollection {
		nodePath = 'assetsSubjectAnnotations'
	}
	assetsPracticeAnnotations = Neos.Neos:ContentCollection {
		nodePath = 'assetsPracticeAnnotations'
	}
	assetsMethodical = Neos.Neos:ContentCollection {
		nodePath = 'assetsMethodical'
	}
	description = Neos.Neos:ContentCollection {
		nodePath = 'description'
	}
	subjects = Neos.Neos:ContentCollection {
		nodePath = 'subjects'
	}
	schedule = Neos.Neos:ContentCollection {
		nodePath = 'schedule'
	}
	documentSubmission = Neos.Neos:ContentCollection {
		nodePath = 'documentSubmission'
	}
	exams = Neos.Neos:ContentCollection {
		nodePath = 'exams'
	}
	payment = Neos.Neos:ContentCollection {
		nodePath = 'payment'
	}
	admission = Neos.Neos:ContentCollection {
		nodePath = 'admission'
	}
	admissionContacts = Neos.Neos:ContentCollection {
		nodePath = 'admissionContacts'
	}
	numberOfSeats = Neos.Neos:ContentCollection {
		nodePath = 'numberOfSeats'
	}
	admissionRegulations = Neos.Neos:ContentCollection {
		nodePath = 'admissionRegulations'
	}
	feedback = Neos.Neos:ContentCollection {
		nodePath = 'feedback'
	}

	@cache {
		mode = 'cached'
		entryIdentifier {
			node = ${node}
			editPreviewMode = ${editPreviewMode}
		}
		entryTags {
			1 = ${'Node_' + node.identifier}
			2 = ${'DescendantOf_' + node.identifier}
		}
	}
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}

prototype(Sfi.Sfi:StudyProgramShort) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/StudyProgramShort.html'
	node = ${node}
	title = ${String.stripTags(q(node).property('title'))}
	level = ${String.stripTags(q(node).parent().parent().property('title'))}
	division = ${String.stripTags(q(node).parent().property('title'))}
	duration = ${q(node).property('duration')}
	nextDate = ${q(node).property('nextDate')}
	educationForm = ${q(node).property('educationForm')}
	details = ${q(node).property('details')}
	price = ${q(node).property('price')}
	noLink = ${q(node).property('noLink')}
	specialityId = ${q(node).property('specialityId')}
}

prototype(Sfi.Sfi:StudyProgramShortFormal) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/StudyProgramShortFormal.html'
	node = ${node}
	title = ${String.stripTags(q(node).property('title'))}
	level = ${q(node).parent().parent().property('title')}
	division = ${q(node).parent().property('title')}
	duration = ${q(node).property('duration')}
	accreditationDate = ${q(node).property('accreditationDate')}
	noLink = ${q(node).property('noLink')}

	#TODO: translate
	assetDescription = Sfi.Sfi:StudyProgramAsset {
		content = 'Описание образовательной программы'
		@override.node = ${q(node).children('assetDescription').get(0)}
	}
	assetsPlan = Neos.Neos:ContentCollection {
		nodePath = 'assetsPlan'
	}
	assetSchedule = Sfi.Sfi:StudyProgramAsset {
		content = 'Календарный учебный график'
		@override.node = ${q(node).children('assetSchedule').get(0)}
	}
	assetAccreditation = Sfi.Sfi:StudyProgramAsset {
		title = 'Свидетельство о государственной аккредитации'
		@override.node = ${q(node).children('assetAccreditation').get(0)}
	}
	assetsSubjectAnnotations = Neos.Neos:ContentCollection {
		nodePath = 'assetsSubjectAnnotations'
	}
	assetsPracticeAnnotations = Neos.Neos:ContentCollection {
		nodePath = 'assetsPracticeAnnotations'
	}
}

prototype(Sfi.Sfi:StudyProgramsListingAbstract) < prototype(Sfi.Shared:Sections) {
	panels.collection = 'to-be-set'

	templatePath = 'resource://Sfi.Shared/Private/Templates/NodeTypes/Sections.html'
	@override.rootNode = ${q(node).property('rootNode')}
	layout = 'Sections--Accordion'
	tabs>
	panels.itemRenderer {
		accordion = ${q(node).property('specialityId') ? q(node).property('title') + ' (' + q(node).property('specialityId') + ')' : q(node).property('title')}
		panel = Sfi.Sfi:StudyProgramShort
	}
}

prototype(Sfi.Sfi:StudyProgramsListing) < prototype(Sfi.Sfi:StudyProgramsListingAbstract) {
	@override.rootNode = ${q(node).property('rootNode')}
	panels.collection = ${q(rootNode).children().get()}
	# Guess why it needs to be overriden?
	templatePath = 'resource://Sfi.Shared/Private/Templates/NodeTypes/Sections.html'

	@cache {
		mode = 'cached'
		entryIdentifier {
			node = ${node}
			editPreviewMode = ${editPreviewMode}
		}
		entryTags {
			1 = ${'Node_' + node.identifier}
			2 = ${'Node_' + rootNode.identifier}
			3 = ${'DescendantOf_' + rootNode.identifier}
		}
	}
}

prototype(Sfi.Sfi:StudyProgramsListingFormal) < prototype(Sfi.Sfi:StudyProgramsListing) {
	templatePath = 'resource://Sfi.Shared/Private/Templates/NodeTypes/Sections.html'
	panels.itemRenderer.panel = Sfi.Sfi:StudyProgramShortFormal
}

prototype(Sfi.Sfi:StudyProgramsListingByReference) < prototype(Sfi.Sfi:StudyProgramsListingAbstract) {
	templatePath = 'resource://Sfi.Shared/Private/Templates/NodeTypes/Sections.html'
	panels.collection = ${q(node).property('references')}
	# Guess why it needs to be overriden?

	@cache {
		mode = 'cached'
		entryIdentifier {
			node = ${node}
			editPreviewMode = ${editPreviewMode}
		}
		entryTags {
			1 = ${'NodeType_Sfi.Sfi:StudyProgram'}
			2 = ${'Node_' + node.identifier}
		}
	}
}

prototype(Neos.Neos:PrimaryContent) {
	StudyProgram {
		@position = 'before default'
		condition = ${q(node).is('[instanceof Sfi.Sfi:StudyProgram]')}
		type = 'Sfi.Sfi:StudyProgram'
	}
}
