prototype(Sfi.Sfi:Promo) {
	title = ${q(node).property('title')}
	teaser = ${q(node).property('teaser')}

	main = TYPO3.Neos:ContentCollection {
		nodePath = 'main'
	}

	moreItems = Sfi.Sfi:PromoList {
		content.collection.@process.filterOutSelf {
			expression = ${q(value).filter('[_name != ' + q(node).property('_name') + ']').get()}
			@position = 'start'
		}
	}
}

prototype(Sfi.Sfi:PromoShort) < prototype(TYPO3.Neos:Content) {
	templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/PromoShort.html'
	title = ${String.stripTags(q(node).property('title'))}
	teaser = ${String.stripTags(q(node).property('teaser'))}
	# first image found becomes the thumbnail image TODO: remove legacy support for coverPhoto
	thumbnailImage = ${q(node).property('image')}
}



prototype(Sfi.Sfi:PromoList) < prototype(Neos.Fusion:Tag) {
	tagName = 'div'
	attributes.class = 'PromoList'
	content = TYPO3.Neos:ContentCollectionRenderer {
		itemRenderer = Sfi.Sfi:PromoShort
		collection = ${q(site).children('promo').children().get()}
	}
	@cache {
		mode = 'cached'
		entryIdentifier {
			node = ${node}
			editPreviewMode = ${editPreviewMode}
		}
		entryTags {
			1 = ${'NodeType_Sfi.Sfi:Promo'}
		}
	}
}

prototype(TYPO3.Neos:PrimaryContent) {
    promo {
        @position = 'before default'
        condition = ${q(node).is('[instanceof Sfi.Sfi:Promo]')}
        type = 'Sfi.Sfi:Promo'
    }
}
