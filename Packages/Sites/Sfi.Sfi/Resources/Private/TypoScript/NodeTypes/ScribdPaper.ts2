prototype(Sfi.Sfi:ScribdPaper) {
	layoutRootPath = 'resource://Sfi.Sfi/Private/Layouts'
	sectionMenu = ${siblingsMenu}
	@process.contentElementWrapping = TYPO3.Neos:ContentElementWrapping
}

prototype(Sfi.Sfi:ScribdList) >
prototype(Sfi.Sfi:ScribdList) < prototype(TYPO3.TypoScript:Collection) {
	collection = ${q(documentNode).children('[instanceof TYPO3.Neos:Shortcut]').get()}
	itemName = 'node'
	itemRenderer = TYPO3.TypoScript:Collection {
		@process.tmpl = ${'<div><h2 class="H3 textAlign-center">' + node.properties.title + '</h2>' + value + '</div>'}
		collection = ${q(node).children('[instanceof Sfi.Sfi:ScribdPaper]').get()}
		itemName = 'node'
		itemRenderer = Sfi.Sfi:ScribdPaperShort
	}

	@process.contentElementWrapping = TYPO3.Neos:ContentElementWrapping
	@cache {
		mode = 'cached'
		entryTags {
			1 = ${'DescendantOf_' + documentNode.identifier}
		}
	}
}

prototype(Sfi.Sfi:ScribdPaperShort) < prototype(TYPO3.TypoScript:Tag) {
	tagName = 'a'
	attributes.href = NodeUri {
		node = ${node}
	}
	content = TYPO3.TypoScript:Tag {
		attributes.class = 'marginVertical-double'
		content = TYPO3.TypoScript:Array {
			title = ${'<div class="H4">' + node.properties.title + '</div>'}
			mentor = ${'<div class="ServiceEm margin-none">Научный руководитель: ' + node.properties.mentor + '</div>'}
			paperName = ${'<h3 class="Paragraph Link margin-none">' + node.properties.paperName + '</h3>'}
		}
	}
}
