prototype(Sfi.Sfi:StudyProgramAsset) <  prototype(TYPO3.TypoScript:Tag) {
	tagName = 'a'
	attributes.class = 'ButtonInline AssetInline'
	attributes.href = ${'asset://' + resourceIdentifier}
	attributes.href.@process.convertUris = TYPO3.Neos:ConvertUris
	@if.isSet = ${resourceIdentifier != null}
}

prototype(Sfi.Sfi:StudyProgram) {
	partialRootPath = 'resource://Sfi.Sfi/Private/Partials'

	level = ${String.stripTags(q(node).parent().parent().property('title'))}
	division = ${String.stripTags(q(node).parent().property('title'))}

	#TODO: translate
	assetDescription = Sfi.Sfi:StudyProgramAsset {
		content = 'Описание образовательной программы'
		@override.resourceIdentifier = ${node.properties.assetDescription.identifier}
	}
	assetPlan = Sfi.Sfi:StudyProgramAsset {
		content = 'Учебный план'
		@override.resourceIdentifier = ${node.properties.assetPlan.identifier}

	}
	assetSchedule = Sfi.Sfi:StudyProgramAsset {
		content = 'Календарный учебный график'
		@override.resourceIdentifier = ${node.properties.assetSchedule.identifier}
	}
	assetAccreditation = Sfi.Sfi:StudyProgramAsset {
		content = 'Свидетельство о государственной аккредитации'
		@override.resourceIdentifier = ${node.properties.assetAccreditation.identifier}
	}
	assetsPracticeAnnotations = Sfi.Sfi:StudyProgramAsset {
		content = ''
		@override.resourceIdentifier = ${node.properties.assetsPracticeAnnotations.identifier}
	}
	assetInvoice = Sfi.Sfi:StudyProgramAsset {
		content = 'Квитанция для оплаты обучения'
		@override.resourceIdentifier = ${node.properties.assetInvoice.identifier}
	}
	assetAdmissionRegulations = Sfi.Sfi:StudyProgramAsset {
		content = 'Правила приема'
		@override.resourceIdentifier = ${node.properties.assetAdmissionRegulations.identifier}
	}
	assetSampleContract = Sfi.Sfi:StudyProgramAsset {
		content = 'Образец договора об оказании платных образовательных услуг'
		@override.resourceIdentifier = ${node.properties.assetSampleContract.identifier}
	}


	assetsSubjectAnnotations = TYPO3.Neos:ContentCollection {
		nodePath = 'assetsSubjectAnnotations'
	}
	assetsMethodical = TYPO3.Neos:ContentCollection {
		nodePath = 'assetsMethodical'
	}
	description = TYPO3.Neos:ContentCollection {
		nodePath = 'description'
	}
	subjects = TYPO3.Neos:ContentCollection {
		nodePath = 'subjects'
	}
	schedule = TYPO3.Neos:ContentCollection {
		nodePath = 'schedule'
	}
	documentSubmission = TYPO3.Neos:ContentCollection {
		nodePath = 'documentSubmission'
	}
	exams = TYPO3.Neos:ContentCollection {
		nodePath = 'exams'
	}

	@process.contentElementWrapping = TYPO3.Neos:ContentElementWrapping
}

prototype(Sfi.Sfi:StudyProgramShort) < prototype(TYPO3.Neos:Content) {
	templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/StudyProgramShort.html'
	title = ${String.stripTags(q(node).property('title'))}
	duration = ${q(node).property('duration')}
	nextDate = ${q(node).property('nextDate')}
	educationForm = ${q(node).property('educationForm')}
	details = ${q(node).property('details')}
	price = ${q(node).property('price')}
}

prototype(Sfi.Sfi:StudyProgramsListingAbstract) < prototype(Sfi.Shared:Sections) {
	panels.collection = 'to-be-set'

	templatePath = 'resource://Sfi.Shared/Private/Templates/NodeTypes/Sections.html'
	@override.rootNode = ${q(node).property('rootNode')}
	layout = 'Sections--Accordion'
	tabs>
	panels.itemRenderer {
		accordion = ${q(node).property('title')}
		panel = Sfi.Sfi:StudyProgramShort
	}
}

prototype(Sfi.Sfi:StudyProgramsListing) < prototype(Sfi.Sfi:StudyProgramsListingAbstract) {
	@override.rootNode = ${q(node).property('rootNode')}
	panels.collection = ${q(rootNode).children().get()}
	# Guess why it needs to be overriden?
	templatePath = 'resource://Sfi.Shared/Private/Templates/NodeTypes/Sections.html'

	@cache {
		mode = 'cached'
		entryIdentifier {
			node = ${node}
			editPreviewMode = ${editPreviewMode}
		}
		entryTags {
			1 = ${'Node_' + rootNode.identifier}
			2 = ${'DescendantOf_' + rootNode.identifier}
		}
	}
}

prototype(Sfi.Sfi:StudyProgramsListingByReference) < prototype(Sfi.Sfi:StudyProgramsListingAbstract) {
	panels.collection = ${q(node).property('references')}
	# Guess why it needs to be overriden?
	templatePath = 'resource://Sfi.Shared/Private/Templates/NodeTypes/Sections.html'

	@cache {
		mode = 'cached'
		entryIdentifier {
			node = ${node}
			editPreviewMode = ${editPreviewMode}
		}
		entryTags {
			1 = ${'Node_' + node.identifier}
			2 = ${'NodeType_Sfi.Sfi:StudyProgram'}
		}
	}
}

prototype(TYPO3.Neos:PrimaryContent) {
	StudyProgram {
		@position = 'before default'
		condition = ${q(node).is('[instanceof Sfi.Sfi:StudyProgram]')}
		type = 'Sfi.Sfi:StudyProgram'
	}
}
