#This is a default page object. It's being extended and never used directly.

prototype(Sfi.Sfi:Page) < prototype(TYPO3.Neos:Page) {
	head {
		titleTag.content = ${String.stripTags(q(node).property('title'))}
		stylesheets.site = TYPO3.TypoScript:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/Pages/Utility.html'
			sectionName = 'stylesheets'
		}
		stylesheets.backend = TYPO3.TypoScript:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/Pages/Utility.html'
			sectionName = 'backendStylesheets'
			@process.1=${node.context.workspaceName != 'live' ? value:''}
		}
		javascripts.headScripts = TYPO3.TypoScript:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/Pages/Utility.html'
			sectionName = 'headScripts'
		}
	}
	bodyTag.attributes.class = 'Page'
	bodyTag.attributes.id = ${q(node).property('_name')}
	body {
		templatePath = 'resource://Sfi.Sfi/Private/Templates/Pages/Default.html'
		partialRootPath = 'resource://Sfi.Sfi/Private/Partials'
		layoutRootPath = 'resource://Sfi.Sfi/Private/Layouts'

		mainMenu = Menu{
			templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/MainMenu.html'
			partialRootPath = 'resource://Sfi.Sfi/Private/Partials'
			entryLevel = 1
			maximumLevels = 2
		}
		secondaryMenu = Menu{
			templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/SecondaryMenu.html'
			itemCollection = ${q(site).children('links').children().get()}
		}
		sectionMenu = Menu
		sectionMenu {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/SectionMenu.html'
			entryLevel = 2
			maximumLevels = 0
		}

		breadcrumbMenu = BreadcrumbMenu{
			attributes.class = 'BreadcrumbMenu'
		}

		# TODO: refactor into FooterGallery object
		footerGallery = Sfi.Sfi:FooterGallery

		content {
			main = PrimaryContent {
				nodePath = 'main'
			}
		}

		javascripts.bodyScripts = TYPO3.TypoScript:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/Pages/Utility.html'
			partialRootPath = 'resource://Sfi.Sfi/Private/Partials'
			sectionName = 'bodyScripts'
		}
		javascripts.backend = TYPO3.TypoScript:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/Pages/Utility.html'
			sectionName = 'backendScripts'
			@process.1=${node.context.workspaceName != 'live' ? value:''}
		}
	}
}
page = Sfi.Sfi:Page

root.page {
	condition = ${q(node).is('[instanceof Sfi.Sfi:Page]')}
	renderPath = '/page'
	@position = 'end 9900'
}