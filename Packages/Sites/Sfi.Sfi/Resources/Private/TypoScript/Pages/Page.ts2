#This is a default page object. It's being extended and never used directly.
prototype(Sfi.Sfi:Page) >
prototype(Sfi.Sfi:Page) < prototype(Neos.Neos:Page) {
	head {
		titleTag.content = ${String.stripTags(q(node).property('title')) + ' | ' + Translation.translate('sfi.abbrev', null, [], null, 'Sfi.Sfi')}

		opengraph = Sfi.News:Opengraph
		opengraph.@if.isNews = ${q(node).is('[instanceof Sfi.News:AbstractArticle]')}

		stylesheets.site = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
			sectionName = 'stylesheets'
		}
		stylesheets.backend = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
			sectionName = 'backendStylesheets'
			@process.1=${node.context.workspaceName != 'live' ? value:''}
		}
		stylesheets.@cache {
			mode = 'cached'
			entryIdentifier.a = 'a'
		}
		javascripts.headScripts = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
			sectionName = 'headScripts'
		}
		javascripts.@cache {
			mode = 'cached'
			entryIdentifier.a = 'a'
		}
	}
	htmlTag.attributes.lang = ${Array.first(node.context.dimensions.language)}
	bodyTag.attributes.class = 'Page'
	bodyTag.attributes.id = ${'page-' + q(node).property('_name')}
	body {
		templatePath = 'resource://Sfi.Sfi/Private/Templates/Pages/Page.html'
		partialRootPath = 'resource://Sfi.Sfi/Private/Partials'
		layoutRootPath = 'resource://Sfi.Sfi/Private/Layouts'

		mainMenu = Menu {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/MainMenu.html'
			partialRootPath = 'resource://Sfi.Sfi/Private/Partials'
			entryLevel = 1
			maximumLevels = 2
		}

		lang = ${Array.first(node.context.dimensions.language)}
		@context.altLang = ${this.lang == 'ru' ? 'en' : 'ru'}

		languageMenu = Sfi.Sfi:LanguageMenu
		subMenu = Sfi.Sfi:SubMenu {
			@if.isMain = ${q(node).is('[instanceof Sfi.Sfi:PageMain]') ? true : false}
		}
		secondaryMenu = Menu {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/SecondaryMenu.html'
			itemCollection = ${q(site).children('links').children().get()}
		}
		@override.sectionMenu = Menu {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/SectionMenu.html'
			entryLevel = 2
			maximumLevels = 0
		}
		@override.siblingsMenu = Menu {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/SectionMenu.html'
			itemCollection = ${q(documentNode).parent().children('[instanceof Neos.Neos:Document]').get()}
		}

		breadcrumbMenu = BreadcrumbMenu {
			attributes.class = 'BreadcrumbMenu hide-for-small-only'
			@if.isNotMain = ${q(node).is('[instanceof Sfi.Sfi:PageMain]') ? false : true}
			@if.isNotNews = ${q(node).is('[instanceof Sfi.News:AbstractNews]') ? false : true}
		}

		footerGallery = Sfi.Sfi:FooterGallery

		main = PrimaryContent {
			renderByType {
				condition = TRUE
				type = ${q(node).property('_nodeType.name')}
			}
			@if.notSubscription = ${request.pluginArguments['newsletter']['@package'] != 'Psmb.Newsletter'}
		}
		subscriptionPlugin = Psmb.Newsletter:SubscriptionPlugin

		javascripts.bodyScripts = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
			sectionName = 'bodyScripts'
		}
		javascripts.analytics = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
			partialRootPath = 'resource://Sfi.Sfi/Private/Partials'
			sectionName = 'analytics'
		}
		javascripts.backend = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
			sectionName = 'backendScripts'
			@process.1 = ${node.context.workspaceName != 'live' ? value:''}
		}
		javascripts.@cache {
			mode = 'cached'
			entryIdentifier.a = 'a'
		}
	}
}

#Needed for listable-paginate widget to work
prototype(Sfi.Sfi:Page).@cache.entryIdentifier.pagination = ${request.pluginArguments.listable-paginate.currentPage}
root.@cache.entryIdentifier.pagination = ${request.pluginArguments.listable-paginate.currentPage}

# Extend StyleguidePage with Sfi.Sfi:Page, to get correct CSS for Styleguide
prototype(Sfi.Styleguide:StyleguidePage) < prototype(Sfi.Sfi:Page) {
	# disable analytics
	body.javascripts.analytics >
}

root.fallbackPage {
	@position = 'before default'
	condition = TRUE
	type = 'Sfi.Sfi:Page'
}

# Disable stupid layout mechanism
root.layout>
